// Calculate ancestry proportion using fixed neutral markers.
function (float)ancestry_prop(lifso<Subpopulation> pop, lifso<MutationType> type) {
    if (chrom == "A")
        n = 2;
    else if (chrom == "X")
        n = 1.5;
    else
        n = 0.5;

    return(sum(pop.individuals.countOfMutationsOfType(type)) / (n * pop.individualCount) / length(marker_pos));
}

// Set a default value of a command-line argument.
function (void)set(s arg, lifs default) {
    if (!exists(arg)) defineConstant(arg, default);
}

// Check if a required command-line argument was specified.
function (void)check_arg(s arg) {
    if (!exists(arg)) stop("Required argument '" + arg + "' not specified!\n");
}

initialize() {
    set("ancestral_Ne", 10000);
    set("human_Ne", 10000);
    set("neand_Ne", 1000);
    set("mut_rate", 0);
    set("chrom", "Y");
    set("chrom_length", 60000000);
    set("gene_count", 100);
    set("dfe_scale", 1.0);

    set("dump_at", 0);

    check_arg("gene_total");
    check_arg("admix_time");
    check_arg("admix_rate");
    check_arg("direction");
    check_arg("sample_for");
    check_arg("output");

    if (direction != "modern2neand" & direction != "neand2modern")
        stop("Incorrect direction of gene flow: '" + direction + "' specified!\n");

    defineConstant("output_traj", output + "_traj.tsv");
    defineConstant("output_pop", output + "_pop.txt");

    marker_count = asInteger(chrom_length / 10000);
    if (chrom == "A") {
        defineConstant("recomb_rate", 1e-8);
    } else if (chrom == "X") {
        defineConstant("recomb_rate", 2/3 * 1e-8);
    } else {
        marker_count = 1;
        defineConstant("recomb_rate", 0.0);
    }

    defineConstant("marker_pos", asInteger(seq(0, chrom_length - 1, length = marker_count)));

    initializeMutationRate(mut_rate);
    initializeRecombinationRate(recomb_rate);

    // neutral ancestry marker
    initializeMutationType("m0", 0.5, "f", 0.0);
    m0.convertToSubstitution = F;

    // deleterious mutation type
    initializeMutationType("m1", 0.5, "g", dfe_scale * -0.043, 0.23);

    // genomic element type of genic regions
    initializeGenomicElementType("g1", m1, 1.0);

    initializeSex(chrom);

    // uniform chromosome with uniformly distributed genic regions
    if (gene_count == 1) {
        starts = 0;
        ends = chrom_length - 1;
    } else {
        gene_length = gene_total / gene_count;
        step = (chrom_length - gene_total) / (gene_count - 1);
        starts = asInteger(seq(0, chrom_length, by = step + gene_length));
        ends = starts + gene_length - 1;
    }
    for (i in 1 : gene_count)
        initializeGenomicElement(g1, asInteger(starts[i - 1]), asInteger(ends[i - 1]));
}

// create a population of human-Neanderthal ancestors
1 {
    sim.addSubpop("p1", ancestral_Ne);

    defineConstant("burnin", 7 * ancestral_Ne);
    defineConstant("human_split", asInteger(600000 / 25));
    defineConstant("nonafr_split", asInteger(70000 / 25));
    defineConstant("introgression", asInteger(admix_time / 25));
    defineConstant("bottleneck", asInteger(1000 / 25)); // duration of the bottleneck

    start = burnin + human_split - introgression;
    defineConstant("sample_end", asInteger(sample_for / 25));
    defineConstant("sampling_gens", start + c(seq(0, 300), seq(400, sample_end, 100)));

    defineConstant("dump_gen", asInteger(dump_at / 25));

    sim.rescheduleScriptBlock(s1, generations = burnin);
    sim.rescheduleScriptBlock(s2, generations = burnin + human_split - nonafr_split);
    sim.rescheduleScriptBlock(s3, generations = burnin + human_split - nonafr_split + bottleneck);
    sim.rescheduleScriptBlock(s4, generations = burnin + human_split - introgression);
    sim.rescheduleScriptBlock(s5, generations = burnin + human_split - introgression + 1);
    sim.rescheduleScriptBlock(s6, generations = sampling_gens);
    if (dump_gen > 0)
        sim.rescheduleScriptBlock(s7, generations = burnin + human_split - dump_gen);
    else
        sim.deregisterScriptBlock(s7);
}

// end of the burn-in period - split of Neanderthals from other humans
s1 2 late() {
    cat("Generation: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- end of burnin phase\n");
    cat("- split of Neanderthals from modern humans\n");

    sim.addSubpop("p2", neand_Ne);

    cat("- placing neutral markers on the Neanderthal lineage\n");
    p2.genomes[!p2.genomes.isNullGenome].addNewDrawnMutation(m0, marker_pos);
}

// split of non-Africans from Africans
s2 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- non-African bottleneck\n");

    p1.setSubpopulationSize(2000);
}

s3 2 { cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- end of the non-African bottleneck\n");

    p1.setSubpopulationSize(human_Ne);
}

s4 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");

    if (direction == "modern2neand") {
        cat("- simulating modern human admixture into Neanderthals: " + admix_rate + "\n");
        p2.setMigrationRates(p1, admix_rate);
    } else {
        cat("- simulating Neanderthal admixture into modern humans: " + admix_rate + "\n");
        p1.setMigrationRates(p2, admix_rate);
    }
}

s5 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- end of introgression\n");

    p1.setMigrationRates(p2, 0);
    p2.setMigrationRates(p1, 0);
}

s6 2 late() {
    post_admixture = sim.generation - (burnin + human_split - introgression);

    if (post_admixture == 0) writeFile(output_traj, "gen\tancestry");

    if (direction == "modern2neand")
        ancestry = 1 - ancestry_prop(p2, m0);
    else
        ancestry = ancestry_prop(p1, m0);

    writeFile(
        output_traj,
        paste(c(post_admixture, ancestry), sep = "\t"),
        append = T
    );

    if (post_admixture == sample_end) {
        cat("\nGeneration: " + sim.generation);
        cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
        cat("- simulation finished\n");

        sim.simulationFinished();
    }
}

s7 2 late() {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("-saving population dump\n");

    sim.outputFull(output_pop);
    system("gzip " + output_pop);
}
