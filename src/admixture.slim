// Calculate ancestry proportion using fixed neutral markers.
function (float)ancestry_prop(lifso<Subpopulation> pop, lifso<MutationType> type) {
    if (chrom_type == "A")
        n = 2;
    else if (chrom_type == "X")
        n = 1.5;
    else
        n = 0.5;

    return(sum(pop.individuals.countOfMutationsOfType(type)) / (n * pop.individualCount) / length(marker_pos));
}

// Set a default value of a command-line argument.
function (void)set(s arg, lifs default) {
    if (!exists(arg)) defineConstant(arg, default);
}

// Check if a required command-line argument was specified.
function (void)check_arg(s arg) {
    if (!exists(arg)) stop("Required argument '" + arg + "' not specified!\n");
}

initialize() {
    set("ancestral_Ne", 10000);
    set("human_Ne", 10000);
    set("archaic_Ne", 1000);
    set("mut_rate", 0);

    check_arg("chrom_type");
    check_arg("chrom_length");
    check_arg("gene_total");
    check_arg("gene_count");
    check_arg("admix_time");
    check_arg("archaic_rate");
    check_arg("modern_rate");
    check_arg("output");

    marker_count = asInteger(chrom_length / 10000);
    if (chrom_type == "A") {
        defineConstant("recomb_rate", 1e-8);
    } else if (chrom_type == "X") {
        defineConstant("recomb_rate", 2/3 * 1e-8);
    } else {
        marker_count = 1;
        defineConstant("recomb_rate", 0.0);
    }

    defineConstant("marker_pos", asInteger(seq(0, chrom_length - 1, length = marker_count)));

    initializeMutationRate(mut_rate);
    initializeRecombinationRate(recomb_rate);

    // neutral ancestry marker
    initializeMutationType("m0", 0.5, "f", 0.0);

    // deleterious mutation type
    initializeMutationType("m1", 0.5, "g", -0.043, 0.23);

    // genomic element type of genic regions
    initializeGenomicElementType("g1", m1, 1.0);

    initializeSex(chrom_type);

    // uniform chromosome with uniformly distributed genic regions
    if (gene_count == 1) {
        starts = 0;
        ends = chrom_length - 1;
    } else {
        gene_length = gene_total / gene_count;
        step = (chrom_length - gene_total) / (gene_count - 1);
        starts = asInteger(seq(0, chrom_length, by = step + gene_length));
        ends = starts + gene_length - 1;
    }
    for (i in 1 : gene_count)
        initializeGenomicElement(g1, asInteger(starts[i - 1]), asInteger(ends[i - 1]));
}

// create a population of human-archaic ancestors
1 {
    sim.addSubpop("p1", ancestral_Ne);

    defineConstant("burnin", 7 * ancestral_Ne);
    defineConstant("human_split", asInteger(600000 / 25));
    defineConstant("nonafr_split", asInteger(70000 / 25));
    defineConstant("introgression", asInteger(admix_time / 25));
    defineConstant("bottleneck", asInteger(1000 / 25)); // duration of the bottleneck

    start = burnin + human_split - introgression;
    defineConstant("sampling", start + c(seq(0, 50), seq(200, introgression, by = 200)));

    sim.rescheduleScriptBlock(s1, generations = burnin);
    sim.rescheduleScriptBlock(s2, generations = burnin + human_split - nonafr_split);
    sim.rescheduleScriptBlock(s3, generations = burnin + human_split - nonafr_split + bottleneck);
    sim.rescheduleScriptBlock(s4, generations = burnin + human_split - introgression - 1);
    sim.rescheduleScriptBlock(s5, generations = burnin + human_split - introgression);
    sim.rescheduleScriptBlock(s6, generations = sampling);
}

// end of the burn-in period - split of archaics from other humans
s1 2 late() {
    cat("Generation: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- split of archaics from modern humans\n");

    sim.addSubpop("p2", archaic_Ne);

    cat("- placing neutral markers on the archaic lineage\n");
    p2.genomes[!p2.genomes.isNullGenome].addNewDrawnMutation(m0, marker_pos);
}

// split of non-Africans from Africans
s2 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- non-African bottleneck\n");

    p1.setSubpopulationSize(2000);
}

s3 2 { cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- end of the non-African bottleneck\n");

    p1.setSubpopulationSize(human_Ne);
}

s4 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- simulating introgression\n");
    cat("\tarchaic admixture into modern humans: " + archaic_rate + "\n");
    cat("\tmodern human admixture into archaics: " + modern_rate + "\n");

    p2.setMigrationRates(p1, modern_rate);
    p1.setMigrationRates(p2, archaic_rate);
}

s5 2 {
    cat("\nGeneration: " + sim.generation);
    cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
    cat("- end of introgression\n");

    p1.setMigrationRates(p2, 0);
    p2.setMigrationRates(p1, 0);
}

s6 2 {
    post_admixture = sim.generation - (burnin + human_split - introgression);

    if (post_admixture == 0) writeFile(output, "gen\tmodern\tarchaic");

    archaic_ancestry = ancestry_prop(p1, m0);
    modern_ancestry = 1 - ancestry_prop(p2, m0);

    writeFile(
        output,
        paste(c(post_admixture, archaic_ancestry, modern_ancestry), sep = "\t"),
        append = T
    );

    if (sim.generation - (burnin + human_split) == 0) {
        cat("\nGeneration: " + sim.generation);
        cat(" (" + 25 * (burnin + human_split - sim.generation) + " kya)\n");
        cat("- simulation finished\n");
    }
}
